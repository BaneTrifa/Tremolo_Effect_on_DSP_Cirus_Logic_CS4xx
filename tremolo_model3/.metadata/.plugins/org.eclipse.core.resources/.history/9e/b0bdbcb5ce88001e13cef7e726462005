_TREMOLO_ASM

	.if defined (_TREMOLO_ASM)

_BLOCK_SIZE .equ 16	

	.public _tremoloLeft
	.public _tremoloRight
    .extern _generate_sine
    .extern _tremolo_postGain
    .extern _limiterThreshold
	
X_SECTION   .xdata_ovly

PI	.dw 0.78539816339725	// PI scaled by 4 (2 bits)


L_SECTION   .data_ovly

phLeft .bss (1)
phRight .bss (1)
PH_STEP .dw (0x00015cf7), (0x51db94e7)	// phase step, 0.0000416=(freq/sample_rate)=(2/48000)

	.code_ovly

  # x1 - parameter phase, DO NOT MODIFY i2 and i3
  # return value: a0 
_lfo:	# formula for lfo is: return 0.5 + 0.5 * gen_sin(2.0 * PI * phase);
	
	//fract_pi = (DSPaccum)PI;	scaled by 4
	x0 = xmem[PI]
	
	//accum_pi = fract_pi * phase;
	a0 = x0 * x1	// multiply phase with PI
	//accum_pi <<= 3;
	a0 = a0 << 1	// resacle by 4
	a0 = a0 << 1
	a0 = a0 << 1	// multiply by 2, IN a0 is now  2*PI*phase

	//accum_pi = (DSPaccum)gen_sin(accum_pi); a0 is return value from gen_sin
	call _generate_sine
	
	//accum_pi >>= 1; 
	a0 = a0 >> 1 // by formula multiply by 0.5

	//accum_pi += (DSPaccum)0.5;
	a1 =(0x4000) // const 0.5
	a0 = a0 + a1	// by formula add 0.5
	
	ret	
	
	
	 # i4 - parameter pointer to in channel (left)
	 # i5 - parameter pointer to out channel (Ls)
	 #return value: None
_tremoloLeft: // DO NOT MODIFY i2 and i3

	
	i6 = (phLeft) + (0)	// memory address where the phase for the left channel is located 
	b0 = xymem[i6]		// load phase for L channel
	x1 = b0				// in x0 is local copy of  the phase
	
	//for (i = 0; i < BLOCK_SIZE; ++i)
	do(_BLOCK_SIZE),>loop
		//accum_lfo = (DSPaccum) lfo(ph); -> return value is a0
		call _lfo
		
		//accum_lfo = (DSPaccum)1.0 - accum_lfo;
		ufixed16(a1) = (0x8000)	// a1 = 1.0
		a1 = a1 - a0
		
		//accum_lfo >>= 1;
		a1 = a1 >> 1	// scale value by 2 
		
		//accum_lfo = (DSPfract)accum_lfo * in;
		x0 = a1
		y0 = ymem[i4]; i4 += 1
		a1 = x0 * y0		// multiply input sample with math calculateted value
		
		//accum_lfo = (DSPfract)accum_lfo * postgain;
		x0 = a1
		y0 = xmem[_tremolo_postGain]
		a1 = x0 * y0	// multiply with tremolo post gain
		
		//accum_lfo <<= 1;
		a1 = a1 << 1	// rescale by 2
		
		// *output = saturation_tremolo(accum_lfo);
		b1 = xmem[_limiterThreshold]	// write output in Ls
		if (b1 < a1) a1 = b1
		
		b1 = -b1
		if (b1 > a1) a1 = b1
		
		ymem[i5] = a1; i5 += 1
		
		// *accum_ph += (DSPaccum)PH_STEP;
		i6 = (phLeft) + (0)	// memory address where the phase for the left channel is located 
		b0 = xymem[i6]	// load phase for left channel (accum_ph)
		
		i6 = (PH_STEP) + (0)	// memory address where the phase step is located 
		a0 = xymem[i6]
		
		b0 = b0 + a0	// update phase
		
		
		//if (*accum_ph >= (DSPaccum)1.0)
		//	*accum_ph -= (DSPaccum)1.0;
		ufixed16(a1) = (0x8000)	// a1 = 1.0
		b0 - a1
		if (b < 0) jmp>write_b0	// // Keep phase in range [0, 1)
			b0 = b0 - a1

		//ph = *accum_ph;
%write_b0:	i6 = (phLeft) + (0)
			xymem[i6] = b0
		
%loop:	x1 = b0

	ret


	# i4 - parameter pointer to in channel (left)
	# i5 - parameter pointer to out channel (Ls)
	#return value: None
_tremoloRight: // DO NOT MODIFY i2 and i3

	i6 = (phRight) + (0)
	b0 = xymem[i6]	// load global phase for left channel
	x1 = b0				// local phase
	
	//for (i = 0; i < BLOCK_SIZE; ++i)
	do(_BLOCK_SIZE),>loop
		//accum_lfo = (DSPaccum) lfo(ph); -> return value is a0
		call _lfo
		
		//accum_lfo = (DSPaccum)1.0 - accum_lfo;
		ufixed16(a1) = (0x8000)	// a1 = 1.0
		a1 = a1 - a0
		
		//accum_lfo >>= 1;
		a1 = a1 >> 1
		
		//accum_lfo = (DSPfract)accum_lfo * in;
		x0 = a1
		y0 = ymem[i4]; i4 += 1
		a1 = x0 * y0
		//accum_lfo = (DSPfract)accum_lfo * postgain;
		x0 = a1
		y0 = xmem[_tremolo_postGain]
		a1 = x0 * y0
		
		//accum_lfo <<= 1;
		a1 = a1 << 1
		
		// *output = saturation_tremolo(accum_lfo);
		b1 = xmem[_limiterThreshold]
		if (b1 < a1) a1 = b1
		
		b1 = -b1
		if (b1 > a1) a1 = b1
		
		ymem[i5] = a1; i5 += 1
		
		// *accum_ph += (DSPaccum)PH_STEP;
		i6 = (phRight) + (0)
		b0 = xymem[i6]	// load accum_ph from L memory
		
		i6 = (PH_STEP) + (0)
		a0 = xymem[i6]
		
		b0 = b0 + a0
		
		//if (*accum_ph >= (DSPaccum)1.0)
		//	*accum_ph -= (DSPaccum)1.0;
		ufixed16(a1) = (0x8000)	// a1 = 1.0
		b0 - a1
		if (b < 0) jmp>write_phase
			b0 = b0 - a1

%write_phase: 	i6 = (phRight) + (0)
				xymem[i6] = b0

		//ph = *accum_ph;
%loop:	x1 = b0

	ret
	
	.endif